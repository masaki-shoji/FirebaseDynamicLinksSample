apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply from: 'keystore.gradle'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.badlogic.masaki.firebasesample"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 6
        versionName "1.0.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {

        debug {
            manifestPlaceholders = [appName: "@string/app_name_debug"]
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"
        }

        release {
            manifestPlaceholders = [appName: "@string/app_name"]
            debuggable false
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    dataBinding {
        enabled true
    }
}

def android_support_version = '28.0.0'
def dagger_version = '2.16'
def fb_version = '16.0.6'

configurations.all {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'com.android.support') {
                details.useVersion "${android_support_version}"
            }
        }
    }
}

kapt {
    generateStubs = true
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:${android_support_version}"
    implementation "com.android.support:cardview-v7:${android_support_version}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "com.google.firebase:firebase-core:${fb_version}"
    implementation "com.google.firebase:firebase-invites:${fb_version}"

    ///// DI /////
    implementation "com.google.dagger:dagger:${dagger_version}"
    implementation "com.google.dagger:dagger-android:${dagger_version}"
    implementation "com.google.dagger:dagger-android-support:${dagger_version}"
    kapt "com.google.dagger:dagger-compiler:${dagger_version}"
    kapt "com.google.dagger:dagger-android-processor:${dagger_version}"

    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:2.19.0"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0-RC1"

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

apply plugin: 'com.google.gms.google-services'
